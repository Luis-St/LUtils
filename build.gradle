plugins {
	id "java"
	id "maven-publish"
	id "com.github.joschi.licenser" version "${Licenser}"
}

repositories {
	mavenCentral()
}

dependencies {
	// Google
	implementation "com.google.guava:guava:${GoogleGuava}" // Utility
	// Apache
	implementation "org.apache.logging.log4j:log4j-api:${Log4jAPI}" // Logging
	implementation "org.apache.logging.log4j:log4j-core:${Log4jCore}" // Logging
	implementation "org.apache.commons:commons-lang3:${ApacheLang}" // Utility
	// Other
	implementation "org.jetbrains:annotations:${JetBrainsAnnotations}" // Annotations
	// Test
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

compileJava.dependsOn(updateLicenses)

license {
	header = file("header.txt")
	include "**/*.java"
	exclude("**/Main.java")
}

test {
	useJUnitPlatform()
}

java {
	withSourcesJar()
	withJavadocJar()
}

javadoc {
	exclude("**/Main.java")
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

tasks.register("run", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.utils.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	standardInput = System.in
	args()
	jvmArgs()
}

def username = ""
def password = ""
def file = file("./../../credentials.properties")
if (file.exists()) {
	def properties = new Properties()
	file.withInputStream {properties.load(it)}
	username = properties.getProperty("username")
	password = properties.getProperty("password")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "net.luis"
			artifactId = "LUtils"
			version = "5.2.0"
			artifact jar
			artifact sourcesJar
			artifact javadocJar
		}
	}
	repositories {
		if (!username.isBlank() && !password.isBlank()) {
			maven {
				url "https://maven.luis-st.net/libraries/"
				credentials.username = username
				credentials.password = password
			}
		} else {
			System.err.println("No credentials provided. Publishing to maven.luis-st.net not possible.")
		}
	}
}

jar {
	exclude("net/luis/utils/Main.class")
	manifest {
		attributes([
			"Built-By": "Luis Staudt",
			"Multi-Release": "true"
		])
	}
}
