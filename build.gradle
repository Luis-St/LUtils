plugins {
	id "java"
	id "maven-publish"
}

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "Java Runtime:" + System.getProperty("java.runtime.version"))

repositories {
	mavenCentral()
}

dependencies {
	// Google
	implementation "com.google.guava:guava:${GoogleGuava}" // Utility
	// Apache
	implementation "org.apache.logging.log4j:log4j-api:${Log4jAPI}" // Logging
	implementation "org.apache.logging.log4j:log4j-core:${Log4jCore}" // Logging
	implementation "org.apache.commons:commons-lang3:${ApacheLang}" // Utility
	// Other
	implementation "org.jetbrains:annotations:${JetBrainsAnnotations}" // Annotations
	// Test
	testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.1"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.1"
}

test {
	useJUnitPlatform()
}

java {
	withSourcesJar()
}

artifacts {
	archives sourcesJar
}

tasks.register("run", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.utils.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "net.luis"
			artifactId = "LUtils"
			version = "3.4"
			artifact jar
			artifact sourcesJar
		}
	}
	repositories {
		maven {
			url uri("${projectDir}/maven/repositories")
		}
	}
}

jar {
	manifest {
		attributes([
				"Built-By"     : "Luis Staudt",
				"Multi-Release": "true"
		])
	}
}
